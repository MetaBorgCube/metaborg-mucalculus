module mucalculus

imports
  
  Common
  
lexical syntax

  Variable = [A-Z]
  
context-free start-symbols

  Start

context-free syntax

  Start = StateFormula

  Action = ID
  
  //Regular formula
  RegularFormula.Action = [[Action]]
  
  RegularFormula.Alt = [[RegularFormula] + [RegularFormula]]
  RegularFormula.Seq = [[RegularFormula].[RegularFormula]]
  
  RegularFormula.TRClosure = [[RegularFormula]*]
  RegularFormula.TClosure = [[RegularFormula]+]
  
  RegularFormula.Nest = [([RegularFormula])]
  
  //State formula
  StateFormula.True = [true]
  StateFormula.False = [false]
  StateFormula.Var = [[Variable]]
  
  StateFormula.Negation = [![StateFormula]]
  
  StateFormula.And = [[StateFormula] && [StateFormula]] {left}
  StateFormula.Or = [[StateFormula] || [StateFormula]] {left}
  
  StateFormula.Implication = [[StateFormula] => [StateFormula]]
  
  StateFormula.Diamond = [<[RegularFormula]>[StateFormula]]
  StateFormula.Box = [\[[RegularFormula]\][StateFormula]]
  
  StateFormula.Mu = [mu [Variable].[StateFormula]]
  StateFormula.Nu = [nu [Variable].[StateFormula]]
  
  StateFormula.Nest = [([StateFormula])]
  
template options

  keyword -/- [a-zA-Z]
  
context-free priorities

     {RegularFormula.TRClosure RegularFormula.TClosure}
   > {RegularFormula.Seq}
   > {RegularFormula.Alt},

     {StateFormula.Negation StateFormula.Diamond StateFormula.Box}
   > {left: StateFormula.And StateFormula.Or}
   > {StateFormula.Implication}
   > {StateFormula.Mu StateFormula.Nu}
